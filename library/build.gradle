plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    defaultConfig {
        namespace = "lab.bhx.action"
        minSdkVersion 14
        targetSdkVersion 33
        compileSdk 33
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }
}

project.afterEvaluate {
    publishing {
        publications {
            aar(MavenPublication) {
                groupId = 'com.github.bhx'
                artifactId = 'SwipeAction'
                version = '1.0.0-SNAPSHOT'

                artifact bundleReleaseAar
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

tasks.register('sourcesJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}

tasks.register('javadoc', Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}
